plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
    id "org.jetbrains.kotlin.kapt" version "1.3.50"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.3.50"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "application"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
    id 'nu.studer.jooq' version '4.1'
    id "org.flywaydb.flyway" version "6.2.4"
}


// 環境ごとに読むコム設定ファイルを切り替える
// 未設定の場合は、開発環境（dev）を読み込む
if (!hasProperty('env')) {
    ext.env = 'dev'
}
apply from: "env/${env}.gradle"


subprojects {

}

ktlint {
    additionalEditorconfigFile.set(file(".editorconfig"))
}

version "0.1"
group "mn.kt.hello.world"

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly
}

dependencies {
    implementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut:micronaut-security-jwt"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut.configuration:micronaut-jdbc-hikari"
    implementation "io.swagger.core.v3:swagger-annotations"
    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "javax.annotation:javax.annotation-api"

    // DB
    runtime 'io.micronaut.configuration:micronaut-jdbc-hikari'
    implementation 'io.micronaut.configuration:micronaut-jooq'
    implementation "org.jooq:jooq-codegen:${jooqVersion}"
    implementation "org.jooq:jooq-meta:${jooqVersion}"
    implementation "mysql:mysql-connector-java:8.0.18"
    implementation 'io.micronaut:micronaut-spring'
    runtime 'org.springframework:spring-jdbc'

    kapt platform("io.micronaut:micronaut-bom:$micronautVersion")
    kapt "io.micronaut:micronaut-inject-java"
    kapt "io.micronaut:micronaut-validation"
    kapt "io.micronaut.configuration:micronaut-openapi"
    kaptTest platform("io.micronaut:micronaut-bom:$micronautVersion")
    kaptTest "io.micronaut:micronaut-inject-java"
    runtimeOnly "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8"
    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"
    testImplementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"


    // jooq
    jooqRuntime "mysql:mysql-connector-java:8.0.18"
    jooqRuntime project(':jooq-custom-strategy')
    jooqRuntime 'org.slf4j:slf4j-simple:1.7.25'

    // test
    testImplementation "com.ninja-squad:DbSetup:2.1.0"
    testImplementation "com.ninja-squad:DbSetup-kotlin:2.1.0"
}

test.classpath += configurations.developmentOnly

mainClassName = "com.ikkun2501.bookmanagement.Application"

test {
    useJUnitPlatform()
}

allOpen {
    annotation("io.micronaut.aop.Around")
}

//////////////////////
// jooq
//////////////////////
def jdbcDriver = "com.mysql.cj.jdbc.Driver"
def jooqDestDir = "$buildDir/jooq-gen/"
def jooqDestPackage = 'com.ikkun2501.bookmanagement.infrastructure.jooq.gen'

jooq {
    version = "${jooqVersion}"
    edition = 'OSS'

    bookmanagement(sourceSets.main) {
        jdbc {
            driver = "${jdbcDriver}"
            url = "${datasource.default.url}"
            user = "${datasource.default.user}"
            password = "${datasource.default.password}"
        }
        generator {
            target {
                packageName = "${jooqDestPackage}"
                directory = "${jooqDestDir}"
            }
            strategy {
                name = 'com.ikkun2501.bookmanagement.infrastructure.SamplePrefixGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.mysql.MySQLDatabase'
                inputSchema = 'bookmanagement'
            }
            generate() {
                daos = true
                immutablePojos = true
                pojosEqualsAndHashCode = true
                generatedAnnotationType = "JAVAX_ANNOTATION_GENERATED"
            }
        }
    }
}

clean {
    delete jooqDestDir
}

compileKotlin {
    // kotlinのコンパイル前にjooqのコードを自動生成1
    dependsOn generateBookmanagementJooqSchemaSource
    sourceSets.main.java.srcDirs(jooqDestDir)

    kotlinOptions {
        jvmTarget = '11'
        //Will retain parameter names for Java reflection
        javaParameters = true
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '11'
        javaParameters = true
    }
}

shadowJar {
    mergeServiceFiles()
}

run.classpath += configurations.developmentOnly
run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
// use JUnit 5 platform
test {
    useJUnitPlatform()
}




//////////////////////
// flyway
//////////////////////

flyway {
    url = "${datasource.default.url}"
    user = "${datasource.default.user}"
    password = "${datasource.default.password}"
    schemas = ['bookmanagement']
}

//if (ext.env == "dev") {
task unitFlywayMigrate(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
    url = "${datasource.unit.url}"
    user = "${datasource.unit.user}"
    password = "${datasource.unit.password}"
    schemas = ['bookmanagement']
}
//}
